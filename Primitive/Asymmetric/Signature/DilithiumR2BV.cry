/*
   This module implements the digital signature scheme Dilithium
   submitted to the NIST post-quantum project.

   See https://pq-crystals.org/dilithium/data/dilithium-specification.pdf

   Copyright (c) 2018, Galois Inc.
   www.cryptol.net
   You can freely use this source code for educational purposes.

*/

/* From now on, I follow the following naming conventions:
* types, modules -- upper camel case
* constants -- upper flat case
* functions, properties -- camel case
* add a space after/before opening/closing bracket of lists, records
* add a space before and after an infix operator
*/

/*Fixes to comply with the round2 submission to NIST*/
//1. Randombytes size changed from SeedBytes to 3*SeedBytes from
//2. Expand Mat definition changes


module Primitive::Asymmetric::Signature::DilithiumR2BV where

import Primitive::Keyless::Hash::keccak as SHA3
import Common::utils
import Common::mod_arith
import Primitive::Asymmetric::Cipher::RSA
import Primitive::Asymmetric::Signature::testDilithium
import Primitive::Asymmetric::Signature::test_latest


// These parameters are fixed for all four recommended implementations
type QBits = 23
type UInt32T = 32
type Q = 0x7fe001 //8380417
type N = 0x100 //256
type D = 0xe
WC = 0x3c //60
GAMMA1 = (`Q - 1)/16 : [_]
GAMMA2 = GAMMA1/2 : [_]
ALPHA = 2 * GAMMA2 : [_]
QINV  = 0x00000000fc7fdfff
type PolT1SizePacked = 288

// Parameters for Number Theoretic Transform (NTT)
ROOTOFUNITY    = 1753 : [QBits]
ROOTOFUNITYINV = 731434 : [QBits]
NINV           = 8347681 : [QBits]

// Parameterize the types based on the following implemenations
/* Weak, Medium, Recommended, and Very high*/
/*parameter
    type k : #
    type constraint (fin k, k>=3, k<=6)
    type l : #
    type constraint (fin l, l>=2, l<=5)
    ETA : [4]
    BETA : [12]
    OMEGA : [8]*/
type K = 4
type L = 3

ETA = 6
BETA = 325
type OMEGA = 80

// Integer polynomials; a superset of ring R referenced in specification
type IntPoly        = [N][QBits]
type IntPolyVec dim = [dim]IntPoly

// Polynomials; a superset of ring R_q referenced in specification
type Poly        = [N][QBits] // This is R_q, actually defines a super set of R_q
type PolyVec dim = [dim]Poly

// Polynomials in NTT representation
type NttPoly          = [N][QBits]
type NttVec dim       = [dim]NttPoly
type NttMat dim1 dim2 = [dim1](NttVec dim2)


type PublicKey = { rho : [256], t1 : IntPolyVec K }
type SecretKey = { rho : [256], key  : [256], tr : [384], s1 : PolyVec L, s2 : PolyVec K, t0 : IntPolyVec K }
type SeedBytes = 32
type CryptoPublicKeyBytes = SeedBytes + (PolT1SizePacked * K)

/* Generate public and private keys */
gen : [3 * SeedBytes * 8] -> (PublicKey, SecretKey)
gen s = (pk, sk)
    where
      [rho, rho', key] = split`{3} s //rho <- {0, 1}^256, K <- {0,1}^256
      a       = expandA (split rho) //A ∈ R^(k×l)_q := expandA(rho)
      s1      = polyUniformEtaV`{dim=L} rho' zero // s1<-s^l_η
      s2      = polyUniformEtaV`{dim=K} rho' `L // s2<-s^k_η
      s1ntt   = map ntt s1
      as1     = map nttInv (nttMatMul a s1ntt)
      t       = paddV as1 s2     //t:= as1 + s2
      (t1,t0) = power2RoundPolyVec t  //(t1, t0):= Power2Round_q(t, d=4)
      tr      = crh (rho#(packT1 t1)) //tr ∈ {0,1}^384 := crh(rho||t1)
      pk      = { rho = rho, t1 = t1 }
      sk      = { rho = rho, key = key, tr = tr, s1 = s1, s2 = s2, t0 = t0 }

// Known Answer Test
// To check this property, it is necessary to import testDilithium

testKeyGen = (pk.rho # (packT1 pk.t1) == testCase1.pk) /\
    ((sk.rho # sk.key # sk.tr # (packPolyEtaVec sk.s1) # (packPolyEtaVec sk.s2) # (packT0 sk.t0)) == testCase1.sk)
    where
    (pk, sk) = gen (join testCase1.seed)

/* Generate signature for message M */
// corresponds to cryptoSign_signature
sign : { mbytes } (fin mbytes, mbytes >= 1) => SecretKey -> [mbytes * 8] -> (PolyVec L, PolyVec K, Poly)
sign sk msg = (final_state.z, h, nttInv final_state.c')
     where
      mat   = expandA (split sk.rho) // A ∈ R^(k×l)_q := expandA(ρ)
      mu    = crh`{b=48+mbytes} (sk.tr # msg) // μ ∈{0,1}^384 := crh(tr||M)
      kappa = zero // κ := 0
      s1'   = map ntt sk.s1
      s2'   = map ntt sk.s2
      t0'   = map ntt (sk.t0)
      //TODO: check t0' back later

      init : SignVarTypes
      init = { z=zero,r0=zero,r1=zero,t0'=t0',c'=zero,w=zero,w1=zero
             ,s1'=s1',s2'=s2',cs2=zero,K=sk.key,mu=mu,A=mat,kappa=kappa }

      final_state = dowhile signCondition signBody init
      ct0         = map nttInv (nttVecConstMul final_state.c' final_state.t0')
      h           = makeHintPolyVec (psubV zero ct0
                              ,paddV (psubV final_state.w final_state.cs2) (ct0))

// cryptoSign appends message at the end of the signature
cryptoSign : {mbytes} (fin mbytes, mbytes >= 1) => SecretKey -> [mbytes*8] -> [(SigSizePacked + mbytes)*8]
cryptoSign sk m = packSig (sign sk m) # m

type SignVarTypes = { z : PolyVec L
                    , r0  : IntPolyVec K
                    , r1  : IntPolyVec K
                    , t0' : NttVec K
                    , c'  : NttPoly
                    , w   : PolyVec K
                    , w1  : IntPolyVec K
                    , s1' : NttVec L
                    , s2' : NttVec K
                    , cs2 : PolyVec K
                    , K   : [256]
                    , mu  : [384]
                    , A   : NttMat K L
                    , kappa: [16]
                    }


signCondition : SignVarTypes -> Bit
signCondition sv = fail1 \/ fail2
                   where
                    fail1 = ((infNormPolyVec (sv.z)) >= (GAMMA1-BETA)) // ||z||_∞ ≥ γ1 - β
                            \/ ((infNormPolyVec sv.r0) >= (GAMMA2-BETA)) // ||r0||_∞ ≥ γ2 - β
                            \/ (sv.r1 != sv.w1) // r1 ≂̸ w1
                    fail2 = ((infNormPolyVec (ct0)) >= GAMMA2) // ||ct0||_∞ ≥ γ2
                            \/ ( (foldl (+) 0 cones) > `OMEGA ) // number of ones in h > ω
                    cones = sum (map (\x -> (map onesBv x)) h)
                    ct0   = map nttInv (nttVecConstMul sv.c' sv.t0')
                    h     = makeHintPolyVec (psubV zero ct0
                                      ,paddV (psubV sv.w sv.cs2) (ct0))
                                      // h := makeHintPolyVec(-ct0, w-cs2+ct0, 2γ2)


signBody : SignVarTypes -> SignVarTypes
signBody sv = {z=z,r0=r0,r1=r1,t0'=sv.t0',c'=c',w=w,w1=w1,s1'=sv.s1',s2'=sv.s2',
                   cs2=cs2,K=sv.K,mu=sv.mu,A=sv.A,kappa=(sv.kappa+1)}
              where
                ρ'  = crh`{b=48+32} (sv.K#sv.mu) // ρ' ∈ {0,1}^384 := crh(K||μ) (or ρ' <- {0,1}^384 for randomized signing)
                y   = expandMask((split ρ'), sv.kappa)// y ∈ S^l_(γ1-1) := expandMask(ρ',κ)
                y'  = map ntt y // checked
                w   = map nttInv (nttMatMul sv.A y') // w := Ay
                w1  = highBitsPolyVec w // w1 := highBitsPolyVec(w, 2γ2)
                c'  = ntt ( hash (sv.mu,w1) ) // c ∈ B_(60) := hash(μ||w1)
                cs1 = map nttInv (nttVecConstMul c' sv.s1') // cs1 := c'.s1'
                cs2 = map nttInv (nttVecConstMul c' sv.s2') // cs2 := c'.s2'
                z   = paddV y cs1 // z:= y+cs1
                (r1, r0) = decomposePolyVec (psubV w cs2) // Decompose_q(w-cs2, 2γ2)




verify : {mbytes} (fin mbytes, mbytes >= 1) => PublicKey -> [mbytes * 8] -> (PolyVec L, PolyVec K, Poly) -> Bit
verify pk msg (z, h, c) = crit1 && crit2 && crit3
       where
        a        = expandA (split pk.rho) // A ∈ R^(k×l)_q := expandA(ρ)
        mu       = crh`{b=48+mbytes} ((crh (pk.rho#(packT1 pk.t1))) # msg) // μ ∈{0,1}^384 := crh(crh(ρ||t1)||M)
        zntt     = map ntt z // NTT(z)
        t1dot2d  = map ntt (map (\p -> map (\x ->  mod_mul(`Q, x, (2^^14))) p) pk.t1) // t1.2^^d where d=14
        nttt1dot2d = map ntt t1dot2d
        ct1ntt   = nttVecConstMul (psub zero c) nttt1dot2d   // -ct1
        az       = nttMatMul a zntt
        w1'      = useHintPolyVec (h, paddV az ct1ntt)
        zint     = z
        zinf     = infNormPolyVec zint // ||z||_∞
        crit1    = zinf < GAMMA1 - BETA
        crit2    = c == hash(mu,w1')
        cones    = sum (map (\x -> (map onesBv x)) h)
        crit3    = ( (foldl (+) 0 cones) > `OMEGA ) // number of 1's in h ≤ ω // TODO: fix this


onesBv : {n} (fin n, n>=1)=> [n] -> [n]
onesBv xs =  l!0
    where l = [0]#[ (if (xs@i) then j+1 else j):[n] | i <- [0..N-1] | j<- l]

//:prove (\x -> (onesBv x) == fromInteger (ones x)):[23]-> Bit

packW : IntPolyVec K -> [4*N*K]
packW w = join [pack_poly p | p <- wBV ]
    where
    wBV = map (\p -> map (\x -> drop`{15} x:[8]) p) w
    pack2 : [2][8] -> [8]
    pack2 xs = xs@0 || (xs@1 << 4)
    pack_poly p = join [ pack2 xs | xs <- split p : [128][2][_] ]
    

packPolyEta : Poly -> [4 * N]
packPolyEta p = join [ packCoeff (p@(2*i+0) % `Q) || ((packCoeff (p@(2*i+1) % `Q)) << 4) | i <- [0..(N/2)-1]:[_][8] ]
    where
    packCoeff : [QBits] -> [8]
    packCoeff num = drop (if num > ETA then `Q + ETA - num else ETA - num)

packPolyEtaVec : {m} (fin m, m >= 1) =>  PolyVec m -> [4*N*m]
packPolyEtaVec pv = join [ packPolyEta p | p <- pv ]
 
type SetaBits = 4
//unpackPolyEta
type PolEtaSizePacked = 128

/*unpackPolyEta :  {m} (fin m, m >= 1) =>[m*PolEtaSizePacked][8] -> PolyVec m
unpackPolyEta pvb =  [ elt a | a <- split pvb:[m][128][8] ]
      where
        elt :[128][8] -> Poly
        elt a = join [ [ zext (x && 0x0F), zext (x >> 4)] | x <- a ]*/

unpackPolyEta : [PolEtaSizePacked][8] -> [N][23]
unpackPolyEta p = join [ [ drop`{9}(`Q + ETA - (zext (x && 0x0F))), drop`{9} (`Q + ETA - (zext (x >> 4))) ] | x <- p ]

unpackPolyEtavec :  {m} (fin m, m >= 1) =>[m][PolEtaSizePacked][8] -> [m][256][23]
unpackPolyEtavec pvb =  [ unpackPolyEta pb | pb <- pvb ]


type PolT0SizePacked = 448
packT0 : IntPolyVec K -> [K*PolT0SizePacked*8]
packT0 t0 = join [ polyT0Pack p | p <- t0 ]

polyT0Pack : IntPoly -> [PolT0SizePacked*8]
polyT0Pack p = join [ join [drop`{15} (tmp p i 0),
                       drop`{15} (((tmp p i 0) >> 8) || ((tmp p i 1) << 6)),
                       drop`{15} ((tmp p i 1) >> 2),
                       drop`{15} (((tmp p i 1) >> 10) || ((tmp p i 2) << 4)),
                       drop`{15} ((tmp p i 2) >> 4),
                       drop`{15} (((tmp p i 2) >> 12) || ((tmp p i 3) << 2)),
                       drop`{15} ((tmp p i 3) >> 6)
                     ] | i <- [0..(N/4-1)] ]
        where
          tmp : IntPoly -> [8] -> [8] -> [23]
          tmp p i j =  (`Q+2^^13 - p@(4*i+j))
          

polyT0Unpack : [PolT0SizePacked][8] -> IntPoly
polyT0Unpack p = join[ [ (tmp p i 0) || (zext ((tmp p i 1) && zext 0x3F)) << 8,
                          (zext ((tmp p i 1) >> 6)) || (zext (tmp p i 2)) << 2 || (zext ((tmp p i 3) && zext 0x0F)) << 10,
                          (zext ((tmp p i 3) >> 4)) || (zext (tmp p i 4)) << 4 || (zext ((tmp p i 5) && zext 0x03)) << 12,
                          (zext ((tmp p i 5) >> 2)) || (zext (tmp p i 6)) << 6
                        ] | i <- [0..(N/4-1)]
                      ]
          where
            tmp :[PolT0SizePacked][8] -> [8] -> [8] -> [23]
            tmp p i j =  zext (2^^13 - p@(7*i+j))


unpackT0 : [K][PolT0SizePacked][8] -> IntPolyVec K
unpackT0 pv = [ (polyT0Unpack pv') | pv' <- pv]


// d must be 14 for this to work, and n must be 256

// polyt1_pack

polyPack : Poly -> [PolT1SizePacked*8]
polyPack p = join [ join (pack8 xs) | xs <- split (map (drop`{14}) p) : [32][8][_]]

pack8 : [8][9] -> [9][8]
pack8 xs = map drop`{1} [ xs@0
                        , xs@0 >> 8 || xs@1 << 1
                        , xs@1 >> 7 || xs@2 << 2
                        , xs@2 >> 6 || xs@3 << 3
                        , xs@3 >> 5 || xs@4 << 4
                        , xs@4 >> 4 || xs@5 << 5
                        , xs@5 >> 3 || xs@6 << 6
                        , xs@6 >> 2 || xs@7 << 7
                        , xs@7 >> 1
                        ]

packT1 : PolyVec K -> [K*PolT1SizePacked*8]
packT1 pvk = (join [ polyPack p | p <- pvk])

// polyT1Unpack

polyT1Unpack : [PolT1SizePacked][8] -> Poly
polyT1Unpack p = map (\x -> zext x) (join [unpack8 xs | xs <- (split p): [32][9][_] ])
    where unpack8: [9][8] -> [8][12]
          unpack8 xs = [  zext (xs@0) || ((zext (xs@1 && 0x01):[12]) << 8)
                        , zext (xs@1 >> 1) || ((zext (xs@2 && 0x03):[12]) << 7)
                        , zext (xs@2 >> 2) || ((zext (xs@3 && 0x07):[12]) << 6)
                        , zext (xs@3 >> 3) || ((zext (xs@4 && 0x0F):[12]) << 5)
                        , zext (xs@4 >> 4) || ((zext (xs@5 && 0x1F):[12]) << 4)
                        , zext (xs@5 >> 5) || ((zext (xs@6 && 0x3F):[12]) << 3)
                        , zext (xs@6 >> 6) || ((zext (xs@7 && 0x7F):[12]) << 2)
                        , zext (xs@7 >> 7) || ((zext (xs@8 && 0xFF):[12]) << 1)
                        ]



unpackT1 : [K][PolT1SizePacked][8] -> PolyVec K
unpackT1  pv = [ (polyT1Unpack p) |  p <- pv]

//packPk

packPk :  [32][8] -> PolyVec K -> [CryptoPublicKeyBytes*8]
packPk rho t1 = (join rho) # (packT1 t1)

//unpackPk

unpackPk : [CryptoPublicKeyBytes][8] -> ([SeedBytes][8], PolyVec K)
unpackPk pk = (take`{32} pk, [polyT1Unpack p | p <- (split (drop`{32} pk):[K][288][_])])

/****Packing SK *********/

type CrhBytes = 48
type CryptoSecretKeyBytes = 2800



//packSk

packSk : [SeedBytes][8] -> [SeedBytes][8] -> [CrhBytes][8] -> PolyVec L -> PolyVec K -> PolyVec K -> [CryptoSecretKeyBytes*8]
packSk rho key tr s1 s2 t0 = (join rho) # (join key) # (join tr)# (packPolyEtaVec s1)#(packPolyEtaVec s2) # (packT0 t0)

//unpackSk


unpackSk : [CryptoSecretKeyBytes][8] -> ([SeedBytes][8], [SeedBytes][8], [CrhBytes][8], PolyVec L, PolyVec K, IntPolyVec K)
unpackSk sk = (sk@@[0..31], sk@@[32..63], sk@@[64..111], s1, s2, t0)// key, tr, s1, s2, t0)
        where  s1 = [ unpackPolyEta p | p <- (split (take`{384} (drop`{112} sk)):[L][128][8])] //@@[112..L * PolEtaSizePacked-1])
               s2 = [ unpackPolyEta p | p <- (split (take`{512} (drop`{384} sk)):[K][128][8])] //sk@@[L * PolEtaSizePacked..(L + K)*PolEtaSizePacked-1]
               split_sk = [[ sk@(i*448 + j) | j<- [0..447]] | i <- [0..3]]
               t0 = unpackT0 split_sk //: [K][PolT0SizePacked][8] //sk@@[(L + K)*PolEtaSizePacked..CryptoSecretKeyBytes]

//property packSk_corr x = unpackSk (split (packSk x.0 x.1 x.2 (intToZPolyVec x.3) (intToZPolyVec x.4) x.5)) == x

type PolZSizePacked = 640

polyZPack : Poly -> [PolZSizePacked][8]
polyZPack p =  join [ (tmp p i) | i <- [0..(N/2)-1]]
      where tmp: Poly -> [8] -> [5][8]
            tmp p i  = [r0, r1, r2, r3, r4]
              where
                    t x = GAMMA1 - 1 - p@(2*i+x)
                    t0 = (t 0) + (((sext (t 0))>>31) && `Q)
                    t1 = (t 1) + (((sext (t 1))>>31) && `Q)
                    r0 = drop t0
                    r1 = drop (t0 >> 8)
                    r2 = drop (t0 >> 16) || drop (t1 << 4)
                    r3 = drop (t1 >> 4)
                    r4 = drop (t1 >> 12)

polyZUnpack : [PolZSizePacked][8] -> [256][32]
polyZUnpack p =  join [ compCoeffs i | i <- [0..(N/2)-1]]
    where compCoeffs : [10] -> [2][32]
          compCoeffs i = [rc0, rc1]
                where
                  c0 = zero # p@(5*i+0) || (zero # p@(5*i+1):[32]) << 8 || (zero # (p@(5*i+2) && 0x0F):[32]) << 16
                  c1 = zero # p@(5*i+2) >> 4 || (zero # p@(5*i+3):[32]) << 4 || (zero # p@(5*i+4):[32]) << 12
                  mc0 = GAMMA1 - 1 - c0 : [32]
                  mc1 = GAMMA1 - 1 - c1 : [32]
                  rc0 = mc0 + ((mc0 >>$ 31) && `Q)
                  rc1 = mc1 + ((mc1 >>$ 31) && `Q)

type SigSizePacked = L * PolZSizePacked + OMEGA + K + N/8 + 8 // 2044

encodec : Poly -> [(N/8+8)*8]
encodec p = (join [ [ p@(8*i+j)!=0 | j <- [0..7] ] | i<- [0..(N/8)-1]])# (join sigSigns)
      where
        signs =[0]# [(if (join qm1coeffsIndcs)@i then s||m else s) | s <- signs | m<- masks| i<-[0..255]]
        masks = [1]# [(if (join nzcoeffsIndcs)@i then (m << 1) else m) | m<-masks | i<-[0..255] ]
        nzcoeffsIndcs =  [ [ p@(8*i+j)!=0 | j <- [0..7] ] | i<- [0..(N/8)-1]]
        qm1coeffsIndcs = [ [ p@(8*i+j)==(`Q-1) | j <- [0..7] ] | i<- [0..(N/8)-1]]
        sig = zero:[N/8+8][8]
        sigSigns = [ (signs!0) >>(8*i) | i<-[0..7]]:[8][8]

packSig : (PolyVec L, PolyVec K, Poly) -> [SigSizePacked*8]
packSig (z, h, c) = s0 # s1 # s2
        where s0 = join [join (polyZPack z') | z' <- z ]
              s2 = encodec c
              s1 = encodeh h
                where
                encodeh : PolyVec K -> [(OMEGA + K)*8]
                encodeh h = join OMEGAUpdates
                  where nzcoeffsIndcs = [ [ (p@j)!=0 | j<-[0..255]]| p<- h]
                        cones = sum (map onesBv nzcoeffsIndcs)
                        sig = zero:[84][8]
                        kUpdates0=[sig]#[(if (nzcoeffsIndcs@0)@i==True then ((update sig' i) i) else ((update sig' i) 0))| i <- [0..255]| sig' <- kUpdates0]
                        kUpdates1=[kUpdates0!0]#[(if (nzcoeffsIndcs@1)@i==True then ((update sig' i) i) else ((update sig' i) 0))| i <- [0..255]| sig' <- kUpdates1]
                        kUpdates2=[kUpdates1!0]#[(if (nzcoeffsIndcs@2)@i==True then ((update sig' i) i) else ((update sig' i) 0))| i <- [0..255]| sig' <- kUpdates2]
                        kUpdates3=[kUpdates2!0]#[(if (nzcoeffsIndcs@3)@i==True then ((update sig' i) i) else ((update sig' i) 0))| i <- [0..255]| sig' <- kUpdates3]
                        OMEGAUpdates = (updates (kUpdates3!0) [`OMEGA+0, `OMEGA+1, `OMEGA+2, `OMEGA+3] [0, 1, 2, 3])

decodec : [(N/8+8)][8] -> Poly
decodec xs = coeffs'!0
  where coeffs = zero : Poly
        signs = [0]#[ ((sext s) || ((sext (xs@(`N/8+i))):[64]) << (8*i)) | i<-[0..7] | s<-signs] :[9][64]
        signs' = [signs!0]#[ (if ((((xs@i) >> j) && 0x01) == 1) then (s >> 1) else s) | j<- [0..255] | s <- signs' | i<- [0..3]]
        coeffs' = [coeffs]#[(if ((((xs@i) >> j) && 0x01)==1) then (update coeff (8*i+j) (1^(-((drop s)&&1))&&(1^(`Q-1)))) else coeff) | i<- [0..7] | j<- [0..255] | s <- signs' | coeff <-coeffs' ]

decodeh: [(OMEGA + K)][8] -> PolyVec K
decodeh xs = pv // it is supposed to be pv'!0
      where
        pv = zero : PolyVec K
        ks = [0]#[xs@(`OMEGA+i) | i<- [0..3]]
        //pv' = [pv] # join [ [ (if j < (xs@(`OMEGA+i)) then (updateCoeffs p i j' 1)  else p) | j' <- [xs@j] ]| j<-ks | i<- [0..3] | p <- pv' ] // TODO: fix this

unpackSig : [SigSizePacked][8] -> ([L][N][32], PolyVec K, Poly)
unpackSig sig = (z, h, c)
        where type zlen = L * PolZSizePacked
              z = [ (polyZUnpack p) | p <- split (sig@@[0..zlen - 1]):[L][PolZSizePacked][8] ]
              h = decodeh (sig@@[zlen..zlen + (OMEGA + K) - 1])
              c = decodec (sig@@[zlen + (OMEGA + K).. SigSizePacked-1])

updateCoeffs :  PolyVec N -> [K] -> [8] -> [QBits] -> PolyVec N
updateCoeffs pv i j a =  upv
      where  upv = (update pv i up)
             up = (update p j a)
             p = pv@i


// Only works when n=256

// This function has been modified in round2
//polyUniformEtaV : {dim} (fin dim, dim>=1,width (dim-1) <= 8) => [256] -> [8] -> PolyVec dim // round1
polyUniformEtaV : {dim} (fin dim, dim>=1,width (dim-1) <= 8) => [256] -> [16] -> PolyVec dim
polyUniformEtaV r nonce = [polyUniformEta (split r) (nonce + j) | j <- [0..(dim-1)]:[dim][16]]

genS : [8] -> [2][4]
genS byte = [t0, t1]
    where
    t0 = drop`{4} (byte && 0x0F)
    t1 = drop`{4} (byte >> 4)

// This function has been modified in round2
//(uint8_t) D means taking 8 LSBs from D
//polyUniformEta : [SeedBytes*8] -> [8] -> Poly
polyUniformEta : [SeedBytes][8] -> [16] -> Poly
polyUniformEta rho nonce = map (\x -> if ((toInteger ETA) - (toInteger x)) < 0 then (fromInteger  (((toInteger ETA) - (toInteger x))+`Q):[23]) else (fromInteger ((toInteger ETA) - (toInteger x)):[23])) (take`{N} withinETA)
    where
        seed = (join rho)#(drop`{8} nonce)# (take`{8} nonce)
        randoms = ((join (SHA3::toBytes (take`{2*8*168} (SHA3::SHAKE128 (join (SHA3::toBytes seed))))))#zero)
        withinETA = randomsInRangeDouble genS (2*ETA) randoms


hash : ([384],IntPolyVec K) -> Poly
hash (mu,w) = (sampleInBall ss samples)
  where
    w_packed = packW w
    randoms  = join (SHA3::toBytes (take`{4352} (SHA3::SHAKE256 (join (SHA3::toBytes (mu#w_packed))))))
    ss       = take`{60}(reverse (join (reverse (split (take`{64} randoms):[8][8]))))

    extByte : [8] -> [8]
    extByte x = x

    samples : [60][8]
    samples = take`{60} (randomsInRanges extByte [196..255] (drop`{64} randoms#zero))


sampleInBall : [60] -> [60][8] -> [256][QBits]
sampleInBall signs jsamples = ys ! 0
              where
              ys = [ zero:[256][QBits]]
                    # [ if i==j then (if [s]==1 then (update y i (`Q-1)) else (update y i 1))  //((-1:Integer)^^(toInteger [s]))
                        else (if [s]==1 then (updates y [j,i] [`Q-1,y@j]) else (updates y [j,i] [1,y@j]))  | i <- [196..255]
                                                                  | j <- jsamples
                                                                  | s <- signs
                                                                  | y <- ys
                      ]


expandMask : ([CrhBytes][8], [16]) -> PolyVec L
expandMask  ( ρ', kappa) = [samplePoly ρ' kappa i `L | i <- [0..(L-1)] ]


samplePoly : [CrhBytes][8] -> [16] -> [2] -> [2] -> Poly
//samplePoly K mu kappa i dim = (map (\x -> (`q+GAMMA1-1-(zero#x))) coeffs)
samplePoly ρ' kappa i dim = (map (\x -> mod_sub(`Q, mod_sub(`Q, GAMMA1, 1), zero#x)) coeffs)
    where
        (upper:[20])      =  drop`{4}(2*GAMMA1-2):[20]
        kappaBytes = join (reverse (split ((zext dim)*kappa+(zext i)):[2][8])) // LEB order
        s          = join (SHA3::toBytes (take`{5*8*136} (SHA3::SHAKE256 (join (SHA3::toBytes ((join ρ')#kappaBytes)))))) // checked
        (coeffs:[N][20])   = take`{N} (randomsInRangeDouble extractPolyElt upper (s#zero))


extractPolyElt : [40] -> [2][20]
extractPolyElt bits = [zero#int1, zero#int2]
                where
                [b0,b1,b2,b3,b4] = [(zero#b):[20] | b <- split bits : [5][8]]
                int1 = b2'*2^^16 + b1*2^^8 +  b0
                int2 = b4*2^^12 + b3*2^^4 +  b2''
                b2'  = b2 && 15
                //b2'' = b2/(16:[20])
                b2'' = b2/16
// seed should be in little endian byte order
crh : {b} (fin b, b>=1) => [b*8] -> [384]
crh seed = join (SHA3::toBytes (take`{48*8} (SHA3::SHAKE256 (join (SHA3::toBytes seed)))))

// Fixed for round2 submission

expandA : [SeedBytes][8] -> [K][L]NttPoly
expandA rho = [ [ (matrixPoly (join rho) i j) | j <- [0..(L - 1)] ] |  i <- [0..(K - 1)] ]

matrixPoly : [SeedBytes*8] -> [width K] -> [width L] -> NttPoly
matrixPoly rho i j = take`{N} [drop`{1} elt | elt <- randomElts ]
            where
            randomElts = randomsInRange extractMatElt ((>=) (`Q)) (s#zero)
            s          = join (SHA3::toBytes (take`{6720} (SHA3::SHAKE128 (join (SHA3::toBytes (rho#b))))))
            //b        = 2^^4*(zero#j:[8])+(zero#i:[8])
            b          =  drop`{16}(((zero#j:[32]) << 8) + (zero#i:[32]))


extractMatElt : [24] -> [24]
extractMatElt rands = zero#(2^^16 * b2' + 2^^8 * b1 + b0):[24]
                where
                  [b0, b1, b2] = [(zero#b):[23] | b <- split`{3} rands]
                  b2' = (2^^23-1) && b2 // Typo in paper; following C implementation


/*

  Supporting Algorithms; see Figure 3

*/

power2Round : [QBits] -> ([QBits],[QBits])
power2Round r = ((r'-r0)/2^^`D,r0) // D = 14
            where r' = mod_pow (`Q, r, 1)
                  r0 = centeredMod r' (2^^`D)

polyPower2Round : {m} [m][QBits] -> ([m][QBits], [m][QBits])
polyPower2Round p = unzip [ power2Round r | r<- p]

power2RoundPolyVec : {dim} (fin dim, dim >= 1) => PolyVec dim -> ([dim]IntPoly,[dim]IntPoly)
power2RoundPolyVec v = unzip (map polyPower2Round v)



// returns high and low bits 
decompose : [QBits] -> ([QBits],[QBits])
decompose r = if (r' - r0) == (`Q - 1) then (0, r0 - 1)
                    else ((r' - r0) / ALPHA, r0)
          where r' =  mod_pow (`Q, r, 1)
                r0 = centeredMod r' ALPHA

// This is verifiably equivalent to the C code.
decomposeNew : [32] -> ([32],[32])
decomposeNew a = (af, b0)
        where t0 = a && `0x0007FFFF
              t1 = t0 + ((a >>$ 19) << 9)
              t2 = t1 - ((ALPHA/2) + 1)
             t3 = t2 + ((t2 >>$ 31) && ALPHA)
              t4 = t3 - ((ALPHA/2) - 1)
              a0 = a - t4
              u0 = a0 - 1
              u1 = u0 >>$ 31
              a1 = (a0 >>$ 19) + 1
              a2 = a1 - (u1 && 1)
              b0 = `Q + t4 - (a2 >>$ 4)
              af = a2 && `0x0000000f

decomposePoly : {m} [m][QBits] -> ([m][QBits], [m][QBits])
decomposePoly p = unzip [decompose x | x <- p]

decomposePolyVec : {n} (fin n, n>=1) => PolyVec n  -> (PolyVec n, PolyVec n)
decomposePolyVec r = unzip [ decomposePoly p | p <- r ]

highBits : [QBits] -> [QBits]
highBits r = (decompose r).0

highBitsPolyVec : {n} (fin n, n>=1) => PolyVec n  -> PolyVec n
highBitsPolyVec rv = r1
          where
            (r1,r0) = decomposePolyVec rv

lowBits : [QBits] -> [QBits]
lowBits r = (decompose r).1

makeHint : ([QBits],[QBits]) -> Bit
makeHint (z,r) = r1 != v1
          where r1 = highBits r
                v1 = highBits r+z

CLEAN_Make_hint: ([QBits],[QBits]) -> Bit
CLEAN_Make_hint (z, r) = if ((z <= GAMMA2) || (z > `Q - GAMMA2) || (z == (`Q - GAMMA2)) && (r == 0)) then False else True

makeHintPolyNew: (Poly, Poly) -> [QBits]
makeHintPolyNew (zp, rp) = foldl (+) 0 [ zext [CLEAN_Make_hint (z, r)] | z <- zp | r <- rp]

makeHintPolyVecNew: {k} (fin k, k>=1) => (PolyVec k, PolyVec K) -> [QBits]
makeHintPolyVecNew (zv, rv) = foldl (+) 0 [makeHintPolyNew (zp, rp) | zp <- zv | rp <- rv ]

makeHintPoly: (Poly, Poly) -> Poly
makeHintPoly (zp, rp) =  drop res
    where res = [0]#[coeff+r | r<- res | coeff<- p]
          p = [zext [CLEAN_Make_hint (z, r)] | z <- zp | r <- rp]

makeHintPolyVec : {k} (fin k, k>=1) => (PolyVec k, PolyVec k) -> PolyVec k
makeHintPolyVec (zv, rv) =  [makeHintPoly (zp, rp) | zp <- zv | rp <- rv ]



useHint : ([QBits],[QBits]) -> [QBits]
useHint  (r,h) = zext (if  ~((h ==0) || (r0+`Q>`Q)) then drop`{19}(r1 - 1) else (if (r0+`Q>`Q) then drop`{19}(r1+1) else drop`{19}(r1 - 1)))
        where
          (r1, r0) = decompose r
          //r1'      = if (r0+`q> `q) then (drop`{19}(r1+1) else drop`{19}(r1 - 1)
useHintPoly: Poly -> Poly -> Poly
useHintPoly rp hp = [ useHint (r, h) | r <- rp | h <- hp ]

useHintPolyVec : {k}(fin k, k>=1) => (PolyVec k, PolyVec k) -> PolyVec k
useHintPolyVec (rv, hv) = [useHintPoly hpoly rpoly
                              | hpoly <- hv
                              | rpoly <- rv
                              ]

/*

  Utility functions

*/


ones : {a} (fin a, a >= 1) => [a] -> Integer
ones bv = foldl (+) 0 bva
      where
      bva = map toInteger (split bv:[_][1])

/*IPtoP : IntPoly -> Poly
IPtoP p = map (drop `{UInt32T-QBits}) p


PtoIP : Poly -> IntPoly
PtoIP p = map (\i -> zero#i) p


IPVtoPV : {dim} (fin dim, dim >= 1) => IntPolyVec dim -> PolyVec dim
IPVtoPV intp = map IPtoP intp


PVtoIPV : {dim} (fin dim, dim >= 1) => PolyVec dim -> IntPolyVec dim
PVtoIPV pvec = map PtoIP pvec*/


centeredMod : [QBits] -> [QBits] -> [QBits]
centeredMod x n = if r > (n/2) then r - n
                    else r
              where r = x % n


randomsInRange : {a} (fin a) => ([a]-> [a]) -> ([a] -> Bit) -> [inf] -> [inf][a]
randomsInRange createElt upperCrit randoms = if (upperCrit elt )
                                then  [ elt ]#(randomsInRange createElt upperCrit randomTail)
                                else randomsInRange createElt upperCrit randomTail
                                where
                                elt        = createElt (take`{a} randoms)
                                randomTail = drop`{a} randoms

randomsInRanges : {a,m} (fin a,fin m, m>=1) => ([a]-> [a]) -> [m][a] -> [inf] -> [inf][a]
randomsInRanges createElt uppers randoms =
                if elt <= head uppers
                then  [ elt ]#(randomsInRanges createElt uppersRotate randomTail)
                else randomsInRanges createElt uppers randomTail
                where
                  elt          = createElt (take`{a} randoms)
                  randomTail   = drop`{a} randoms
                  uppersRotate = uppers <<< 1


randomsInRangeDouble : {a} (fin a) => ([2*a]-> [2][a]) -> [a] -> [inf] -> [inf][a]
randomsInRangeDouble createElt upper randoms =
                if (elts@0 <= upper) /\ (elts@1 <= upper)
                then  elts#(randomsInRangeDouble createElt upper randomTail)
                else
                  if (elts@0 <= upper)
                  then [ elts@0 ]#(randomsInRangeDouble createElt upper randomTail)
                  else
                    if (elts@1 <= upper)
                    then [ elts@1 ]#(randomsInRangeDouble createElt upper randomTail)
                    else randomsInRangeDouble createElt upper randomTail
                where
                  elts       = createElt (take`{2*a} randoms)
                  randomTail = drop`{2*a} randoms
/*
  Supporting polynomial and polynomial vector functions
*/

modSum : {s, m} (fin s, fin m) => ([s], [m][s]) -> [s]
modSum (p, l) = sums!0
    where
    sums = [zero]#[mod_add`{s}(p, x, y) | x <- l | y <- sums]

ntt : Poly -> NttPoly
ntt p = nttp
    where
    r k = mod_pow(`Q, ROOTOFUNITY, k)
    exponents = join [ [zext i, (256+(zext i))] | i <- [reverse ((128+i) : [8]) | i <- [0..127] ] ]
    nttp = [(modSum (`Q, [ (mod_mul(`Q, (p@idx), (mod_pow (`Q, (r k), i)))) | idx<-[0..(N-1)]:[N][width N] | i<-[0..(N-1)]:[N][23]]))
           | k <- exponents
           ]

nttInv : NttPoly -> Poly
nttInv p' = p
    where
        pu = reorder p'
        r' i = mod_pow (`Q, ROOTOFUNITYINV, (i*2))
        p = [ mod_mul(`Q, NINV, mod_mul(`Q, mod_pow(`Q, ROOTOFUNITYINV, i), (modSum (`Q, [mod_mul(`Q, (pu@jdx), (r' ((i*j))))
            | jdx<-[0..(N-1)]:[N][width N]
            | j<-[0..(N-1)]:[N][QBits]
            ]))))
            | i <- [0..255]:[N][QBits]
            ]

// Dilithium uses a non-standard ordering of elements in the NTT domain; this function reorders the elements.
reorder : NttPoly -> NttPoly
reorder a = join [ [a1 @ i, a2 @ i] | i <- [0..127]:[_][8] ]
        where
          a1 = a @@ [ (i-1)/2 | i <- [reverse ((128+i) : [8]) | i <- [0..127] ] ]
          a2 = reverse (a @@ [ (511 - (zero#i):[9])/2 | i <- [reverse ((128+i) : [8]) | i <- [0..127] ] ])



// Operations on NTT polynomials

nttMatMul : {dim1,dim2} (fin dim1,fin dim2) => NttMat dim1 dim2 -> NttVec dim2 -> NttVec dim1
nttMatMul mat vec = [ nttDotProduct v vec | v <- mat ]

nttDotProduct : {dim} (fin dim) => NttVec dim -> NttVec dim -> NttPoly
nttDotProduct v1 v2 = foldl padd (zero:NttPoly) [ nttPolyMult p1 p2 | p1 <- v1 | p2 <- v2 ]

nttPolyMult : NttPoly -> NttPoly -> NttPoly
nttPolyMult p1 p2 = [ mod_mul(`Q, x, y) | x <- p1 | y <- p2]

nttVecConstMul : {dim} (fin dim) => NttPoly -> NttVec dim -> NttVec dim
nttVecConstMul c v = map (nttPolyMult c) v

polyVecSum :  {dim} (fin dim) => PolyVec dim -> PolyVec dim -> PolyVec dim
polyVecSum v1 v2 = map nttInv (paddV v1' v2')
        where
        v1' = map ntt v1
        v2' = map ntt v2

// Polynomial addition and subtraction
padd : [N][QBits] -> [N][QBits] -> [N][QBits]
padd p1 p2 = [ mod_add(`Q, x, y) | x <- p1 | y <- p2 ]

psub : [N][QBits] -> [N][QBits] -> [N][QBits]
psub p1 p2 = [ mod_sub(`Q, x, y) | x <- p1 | y <- p2 ]

psubV : {dim} (fin dim) => [dim][N][QBits] -> [dim][N][QBits] -> [dim][N][QBits]
psubV v1 v2 = zipWith psub v1 v2


paddV : {dim} (fin dim) => [dim][N][QBits] -> [dim][N][QBits] -> [dim][N][QBits]
paddV v1 v2 = zipWith padd v1 v2

// Aux functions

infNorm : [QBits] -> [QBits]
infNorm x =  (centeredMod x `Q)

infNormPoly : IntPoly -> [QBits]
infNormPoly p = foldl max 0 p_centered
            where
              p_centered = map infNorm p

infNormPolyVec : {dim} (fin dim, dim>=1) => IntPolyVec dim -> [QBits]
infNormPolyVec v = foldl max 0 (map infNormPoly v)


/************************************/
/* Conversion functions helpful in Testing */
/************************************/

// (Z Q) to bit-vector type

fromZPoly : [N](Z Q) -> [N][width Q]
fromZPoly p = map (\x -> ZtoBV x) p

fromZPolyVec : {dim} (fin dim, dim >= 1) => [dim][N](Z Q) -> [dim][N][width Q]
fromZPolyVec pvec = map (\x -> fromZPoly x) pvec

fromZNttMat : {dim1, dim2} (fin dim1, dim1>=1, fin dim2, dim2>=1) => [dim1][dim2][N](Z Q) -> [dim1][dim2][N][width Q]
fromZNttMat nttMat = map (\x -> fromZPolyVec x) nttMat

// Integer to Bit-Vector type

intToZPoly : [N]Integer -> [N][width Q]
intToZPoly p = map (\x -> fromInteger x) p

intToZPolyVec : {dim} (fin dim, dim >= 1) => [dim][N]Integer -> [dim][N][width Q]
intToZPolyVec pvec = map (\x -> intToZPoly x) pvec

intToZNttMat : {dim1, dim2} (fin dim1, dim1>=1, fin dim2, dim2>=1) => [dim1][dim2][N]Integer -> [dim1][dim2][N][width Q]
intToZNttMat nttMat = map (\x -> intToZPolyVec x) nttMat

//Bit-Vector to Integer type

bvToIntPoly : Poly -> [N]Integer
bvToIntPoly p = map (\x -> toInteger x) p

bvToIntPolyVec : {dim} (fin dim, dim >= 1) => PolyVec dim -> [dim][N]Integer
bvToIntPolyVec pvec = map (\x -> bvToIntPoly x) pvec

bvToIntNttMat : {dim1, dim2} (fin dim1, dim1>=1, fin dim2, dim2>=1) => NttMat dim1 dim2 -> [dim1][dim2][N]Integer
bvToIntNttMat nttMat = map (\x -> bvToIntPolyVec x) nttMat

//Bit-Vector to (Z Q) type

bvToZPoly : Poly -> [N](Z Q)
bvToZPoly p = map (\x -> BVtoZ`{Q} x) p

bvToZPolyVec : {dim} (fin dim, dim >= 1) => PolyVec dim -> [dim][N](Z Q)
bvToZPolyVec pvec = map (\x -> bvToZPoly x) pvec

bvToZNttMat : {dim1, dim2} (fin dim1, dim1>=1, fin dim2, dim2>=1) => NttMat dim1 dim2 -> [dim1][dim2][N](Z Q)
bvToZNttMat nttMat = map (\x -> bvToZPolyVec x) nttMat

// Integer to Bit-Vector type

/*intToZPoly : [N]Integer -> [N](Z Q)
intToZPoly p = map (\x -> fromInteger x:(Z Q)) p

intToZPolyVec : {dim} (fin dim, dim >= 1) => [dim][N]Integer -> [dim][N](Z Q)
intToZPolyVec pvec = map (\x -> intToZPoly x) pvec

intToZNttMat : {dim1, dim2} (fin dim1, dim1>=1, fin dim2, dim2>=1) => [dim1][dim2][N]Integer -> [dim1][dim2][N](Z Q)
intToZNttMat NttMat = map (\x -> intToZPolyVec x) NttMat*/

// (Z Q) to Integer type

/*fromZPoly : [N](Z Q) -> [N]Integer
fromZPoly p = map (\x -> fromZ x) p

fromZPolyVec : {dim} (fin dim, dim >= 1) => [dim][N](Z Q) -> [dim][N]Integer
fromZPolyVec pvec = map (\x -> fromZPoly x) pvec

fromZNttMat : {dim1, dim2} (fin dim1, dim1 >= 1, fin dim2, dim2 >= 1) => [dim1][dim2][N](Z Q) -> [dim1][dim2][N]Integer
fromZNttMat NttMat = map (\x -> fromZPolyVec x) NttMat*/

